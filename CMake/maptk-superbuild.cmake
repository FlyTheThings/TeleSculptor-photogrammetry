# Central location for MAPTK external dependency declaration and resolution
include(ExternalProject)

###
# Option for CUDA
#
option(MAPTK_BUILD_WITH_CUDA "Build with CUDA support" FALSE)
if (MAPTK_BUILD_WITH_CUDA)
  find_package( CUDA QUIET REQUIRED )
endif()

###
# Option for Python
#
option(MAPTK_BUILD_WITH_PYTHON "Build with Python support" FALSE)
if (MAPTK_BUILD_WITH_PYTHON)
  find_package(PythonInterp 3.4 QUIET REQUIRED )
  find_package(PythonLibs 3.4 QUIET REQUIRED )
endif()

###
# Dependencies
#
include(CMake/maptk-external-fletch.cmake)
include(CMake/maptk-external-kwiver.cmake)

###
# MAPTK
#
ExternalProject_Add(maptk
  PREFIX ${MAPTK_BINARY_DIR}
  DEPENDS ${MAPTK_DEPENDENCIES}
  SOURCE_DIR ${MAPTK_SOURCE_DIR}
  BINARY_DIR ${MAPTK_EXTERNAL_DIR}/maptk-build
  STAMP_DIR ${MAPTK_STAMP_DIR}
  CMAKE_CACHE_ARGS
    -Dkwiver_DIR:PATH=${kwiver_DIR}
    -DqtExtensions_DIR:PATH=${qtExtensions_DIR}
    -DBUILD_SHARED_LIBS:BOOL=ON
    -DMAPTK_SUPERBUILD:BOOL=OFF
    -DMAPTK_ENABLE_TESTING:BOOL=${MAPTK_ENABLE_TESTING}
    -DMAPTK_FIXUP_BUNDLE_ON_PACKAGE:BOOL=ON
    -DCMAKE_PREFIX_PATH:STRING=${CMAKE_PREFIX_PATH}
    -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
    -DCMAKE_CONFIGURATION_TYPES:STRING=${CMAKE_CONFIGURATION_TYPES}
    -DCMAKE_CXX_COMPILER:FILEPATH=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_C_COMPILER:FILEPATH=${CMAKE_C_COMPILER}
    -DCMAKE_C_FLAGS:STRING=${ep_common_c_flags}
     ${CMAKE_CXX_COMPILER_LAUNCHER_FLAG}
     ${CMAKE_C_COMPILER_LAUNCHER_FLAG}
    -DCMAKE_EXE_LINKER_FLAGS:STRING=${CMAKE_EXE_LINKER_FLAGS}
    -DCMAKE_SHARED_LINKER_FLAGS:STRING=${CMAKE_SHARED_LINKER_FLAGS}
    -DADDITIONAL_C_FLAGS:STRING=${ADDITIONAL_C_FLAGS}
    -DADDITIONAL_CXX_FLAGS:STRING=${ADDITIONAL_CXX_FLAGS}
  INSTALL_COMMAND ${CMAKE_CPACK_COMMAND}
)

